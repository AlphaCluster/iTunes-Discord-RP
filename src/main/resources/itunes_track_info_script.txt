set STR_INACTIVE to "INACTIVE"
set STR_STOPPED to "STOPPED"
set STR_PLAYING to "PLAYING"
set STR_PAUSED to "PAUSED"

set state to ""
set currentState to ""

property savedSongName : missing value
property savedPlayerState : missing value

repeat
    delay 1
    # If iTunes is not running, set the state to "INACTIVE" and do nothing
    if application "iTunes" is not running then
        if state is not STR_INACTIVE then
            set state to STR_INACTIVE
            log state
        end if
    else
        set somethingChanged to false
        
        # If iTunes is running, execute the logic inside a try block so that, if iTunes is closed during the execution, the script doesn't launch an exception
        try
            tell application "iTunes"
                set currentPlayerState to player state
                if currentPlayerState is not playing and currentPlayerState is not paused then
                    # If there's no song playing or paused, set the state to "PAUSED" and do nothing
                    if state is not STR_STOPPED then
                        set state to STR_STOPPED
                        log state
                    end if
                else
                    set currentTrack to current track
                    set currentSongName to name of currentTrack
                    
                    # If the current song changed, set the flag to "true"
                    if currentPlayerState is not savedPlayerState or currentSongName is not savedSongName then
                        set somethingChanged to true
                    end if
                    
                    # If the flag is "true", log the new track information
                    if somethingChanged then
                        if currentPlayerState is playing then
                            set currentState to STR_PLAYING
                        else
                            set currentState to STR_PAUSED
                        end if
                        # player position & ";;" & duration of currentTrack & ";;" &
                        log currentSongName & ";;" & artist of currentTrack & ";;" & album of currentTrack & ";;" & currentState
                    end if
                    
                    # Update the player state and song name to detect when the track changes
                    set savedPlayerState to currentPlayerState
                    set savedSongName to currentSongName
                end if
            end tell
        on error
            # If iTunes is closed, set the state to "INACTIVE" and do nothing
            set state to STR_INACTIVE
            log state
        end try
    end if
end repeat
